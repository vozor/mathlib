/-
Copyright (c) 2020 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth
-/
import geometry.manifold.charted_space
import analysis.normed_space.inner_product


/-!
# Manifold structure on the sphere

This file defines stereographic projection from the sphere in an inner product space `E`, and uses
it to put a smooth manifold structure on the sphere.

-/

noncomputable theory

open metric

namespace is_R_or_C
/-! Lemmas for `data.complex.is_R_or_C`. -/

variables {ğ•œ : Type*} [is_R_or_C ğ•œ]

lemma im_eq_zero_of_le {a : ğ•œ} (h : abs a â‰¤ re a) : im a = 0 :=
begin
  rw â† zero_eq_mul_self,
  have : re a * re a = re a * re a + im a * im a,
  { convert is_R_or_C.mul_self_abs a;
    linarith [re_le_abs a] },
  linarith
end

lemma re_eq_self_of_le {a : ğ•œ} (h : abs a â‰¤ re a) : â†‘(re a) = a :=
by { rw â† re_add_im a, simp [im_eq_zero_of_le h] }

end is_R_or_C

namespace inner_product_space
/-! Lemmas for `analysis.normed_space.inner_product`. -/

variables {ğ•œ : Type*} [is_R_or_C ğ•œ]
variables {E : Type*} [inner_product_space ğ•œ E]

open is_R_or_C


abbreviation orthogonal_projection_compl [complete_space E] (K : submodule ğ•œ E) :
  E â†’L[ğ•œ] K.orthogonal :=
orthogonal_projection K.orthogonal

/-- The orthogonal projection is the unique point in `K` with the orthogonality property, variant
characterization in terms of the orthogonal complement. -/
lemma eq_orthogonal_projection_of_mem_of_inner_eq_zero' {K : submodule ğ•œ E} [complete_space K]
  {u v : E} (hv : v âˆˆ K) (hvo : u - v âˆˆ K.orthogonal) :
  v = orthogonal_projection K u :=
begin
  apply eq_orthogonal_projection_fn_of_mem_of_inner_eq_zero hv,
  intros w hw,
  rw inner_eq_zero_sym,
  exact hvo w hw
end

lemma eq_proj_of_split (K : submodule ğ•œ E) [complete_space K]
  {v y z : E} (hy : y âˆˆ K) (hz : z âˆˆ K.orthogonal) (hv : y + z = v) :
  y = orthogonal_projection K v :=
begin
  apply eq_orthogonal_projection_of_mem_of_inner_eq_zero' hy,
  convert hz,
  rw â† hv,
  abel
end

lemma eq_proj_of_split' [complete_space E] (K : submodule ğ•œ E) [complete_space K]
  {v y z : E} (hy : y âˆˆ K) (hz : z âˆˆ K.orthogonal) (hv : y + z = v) :
  z = orthogonal_projection_compl K v  :=
begin
  suffices hy' : y âˆˆ K.orthogonal.orthogonal, -- : set E), v = y' + z,
  { rw add_comm at hv,
    exact eq_proj_of_split K.orthogonal hz hy' hv },
  simp [hy]
end

lemma sum_proj' [complete_space E] {K : submodule ğ•œ E} [complete_space K] (w : E) :
  â†‘(orthogonal_projection K w) + â†‘(orthogonal_projection_compl K w) = w :=
begin
  obtain âŸ¨y, hy, z, hz, hwyzâŸ© := K.exists_sum_mem_mem_orthogonal w,
  convert hwyz,
  { rw eq_proj_of_split K hy hz hwyz },
  { rw eq_proj_of_split' K hy hz hwyz }
end


lemma sum_proj [complete_space E] (K : submodule ğ•œ E) [complete_space K] :
  K.subtype_continuous.comp (orthogonal_projection K)
  + K.orthogonal.subtype_continuous.comp (orthogonal_projection_compl K)
  = continuous_linear_map.id ğ•œ E :=
by { ext w, exact sum_proj' w }

lemma proj_perp [complete_space E] (K : submodule ğ•œ E) [complete_space K] (w : E) :
  @inner ğ•œ _ _ (orthogonal_projection K w : E) â†‘(orthogonal_projection_compl K w) = 0 :=
(orthogonal_projection_compl K w).2 _ (orthogonal_projection K w).2

lemma pyth_proj [complete_space E] {K : submodule ğ•œ E} [complete_space K] (w : E) :
  âˆ¥wâˆ¥ * âˆ¥wâˆ¥ = âˆ¥orthogonal_projection K wâˆ¥ * âˆ¥orthogonal_projection K wâˆ¥
    + âˆ¥orthogonal_projection_compl K wâˆ¥ * âˆ¥orthogonal_projection_compl K wâˆ¥:=
begin
  convert norm_add_square_eq_norm_square_add_norm_square_of_inner_eq_zero _ _ (proj_perp K w);
  simp [sum_proj']
end

lemma pyth_proj_sq [complete_space E] {K : submodule ğ•œ E} [complete_space K] (w : E) :
  âˆ¥wâˆ¥ ^ 2 = âˆ¥orthogonal_projection K wâˆ¥ ^ 2 + âˆ¥orthogonal_projection_compl K wâˆ¥ ^ 2:=
begin
  convert @pyth_proj _ _ _ _ _ K _ w;
  simp [pow_two]
end

include ğ•œ

lemma norm_sub_crossmul (v x : E) :
  âˆ¥(âˆ¥vâˆ¥:ğ•œ) â€¢ x - (âˆ¥xâˆ¥:ğ•œ) â€¢ vâˆ¥ * âˆ¥(âˆ¥vâˆ¥:ğ•œ) â€¢ x - (âˆ¥xâˆ¥:ğ•œ) â€¢ vâˆ¥
  = 2 * âˆ¥xâˆ¥ * âˆ¥vâˆ¥ * (âˆ¥xâˆ¥ * âˆ¥vâˆ¥ - re (@inner ğ•œ _ _ x v)) :=
begin
  rw norm_sub_mul_self,
  simp [inner_smul_left, inner_smul_right, norm_smul, is_R_or_C.norm_eq_abs],
  ring
end

lemma norm_sub_crossmul' (v x : E) :
  âˆ¥(âˆ¥vâˆ¥:ğ•œ) â€¢ x - (âˆ¥xâˆ¥:ğ•œ) â€¢ vâˆ¥ ^ 2
  = 2 * âˆ¥xâˆ¥ * âˆ¥vâˆ¥ * (âˆ¥xâˆ¥ * âˆ¥vâˆ¥ - re (@inner ğ•œ _ _ x v)) :=
by { convert norm_sub_crossmul v x, exact pow_two _ }


lemma inner_eq_norm_mul_iff {v x : E}:
  inner v x = (âˆ¥xâˆ¥ : ğ•œ) * âˆ¥vâˆ¥ â†” (âˆ¥xâˆ¥ : ğ•œ) â€¢ v = (âˆ¥vâˆ¥ : ğ•œ) â€¢ x :=
begin
  transitivity âˆ¥(âˆ¥xâˆ¥ : ğ•œ) â€¢ v - (âˆ¥vâˆ¥ : ğ•œ) â€¢ xâˆ¥ ^ 2 = 0,
  { rw norm_sub_crossmul' x v,
    split,
    { intros hxv,
      rw hxv,
      simp only [mul_re, norm_eq_zero, of_real_re, sub_zero, mul_zero, of_real_im],
      ring },
    { simp [is_R_or_C.two_ne_zero],
      rintros ((h | h )| h),
      { simp [h] },
      { simp [h] },
      have : abs (@inner ğ•œ _ _ v x) â‰¤ re (@inner ğ•œ _ _ v x),
      { have := @abs_inner_le_norm ğ•œ _ _ _ v x,
        linarith },
      rw â† re_eq_self_of_le this,
      norm_cast,
      linarith } },
  { split,
    { intros h,
      apply eq_of_norm_sub_eq_zero,
      apply pow_eq_zero h },
    { intros h,
      simp [h] } }
end

lemma inner_eq_norm_mul_iff_of_mem_sphere {v x : E} (hv : âˆ¥vâˆ¥ = 1) (hx : âˆ¥xâˆ¥ = 1) :
  @inner ğ•œ _ _ v x = 1 â†” x = v :=
begin
  transitivity v = x,
  { convert inner_eq_norm_mul_iff using 2;
    simp [hv, hx] },
  exact eq_comm
end

end inner_product_space


namespace inner_product_space
/-! Reals-specific lemmas for `analysis.normed_space.inner_product`. -/

variables {E : Type*} [inner_product_space â„ E]

lemma inner_product_space.mem_sphere (v w : E) (r : â„) : w âˆˆ sphere v r â†” âˆ¥w - vâˆ¥ = r :=
by simp [dist_eq_norm]

-- lemma inner_product_space.sphere_prop (v : E) (r : â„) (w : sphere v r) : âˆ¥â†‘w - vâˆ¥ = r :=
-- by simp [inner_product_space.mem_sphere v w r]

lemma inner_product_space.mem_sphere_zero {w : E} {r : â„} : w âˆˆ sphere (0:E) r â†” âˆ¥wâˆ¥ = r :=
by simp [dist_eq_norm]


lemma inner_eq_norm_mul_iff_real (v x : E) :
  inner v x = âˆ¥xâˆ¥ * âˆ¥vâˆ¥ â†” âˆ¥xâˆ¥ â€¢ v = âˆ¥vâˆ¥ â€¢ x :=
inner_eq_norm_mul_iff

example {v x : E} (hxv : âŸªv, xâŸ«_â„ = âˆ¥xâˆ¥ * âˆ¥vâˆ¥) :
  âˆ¥vâˆ¥ â€¢ x = âˆ¥xâˆ¥ â€¢ v :=
by { rw inner_eq_norm_mul_iff_real at hxv, simp [hxv] }

lemma inner_ne_norm_mul_iff_real (v x : E) :
  inner v x < âˆ¥xâˆ¥ * âˆ¥vâˆ¥ â†” âˆ¥xâˆ¥ â€¢ v â‰  âˆ¥vâˆ¥ â€¢ x :=
begin
  have : _ â†” (_ â‰  _):= not_congr (inner_eq_norm_mul_iff_real v x),
  rw â† this,
  refine âŸ¨ne_of_lt, lt_of_le_of_ne _âŸ©,
  rw mul_comm,
  refine le_trans _ (abs_real_inner_le_norm v x),
  exact le_abs_self _,
end


lemma inner_lt_one_iff_of_mem_sphere {v x : E} (hv : âˆ¥vâˆ¥ = 1) (hx : âˆ¥xâˆ¥ = 1) :
  âŸªv, xâŸ«_â„ < 1 â†” x â‰  v :=
begin
  transitivity v â‰  x,
  { convert inner_ne_norm_mul_iff_real v x;
    simp [hv, hx] },
  exact ne_comm
end


end inner_product_space


section
/-! Lemmas for `algebra.ordered_field` and similar. -/

variables {Î± : Type*} [linear_ordered_field Î±]

/- this lemma would work for `ordered_integral_domain`, if that typeclass existed -/
@[simp] lemma eq_of_pow_two_eq_pow_two {a b : Î±} (ha : 0 â‰¤ a) (hb : 0 â‰¤ b) : a ^ 2 = b ^ 2 â†” a = b :=
begin
  split,
  { intros h,
    refine (eq_or_eq_neg_of_pow_two_eq_pow_two _ _ h).elim id _,
    intros h',
    linarith },
  { rintros rfl,
    simp },
end

lemma foo (a : Î±) {b : Î±} (hb : 0 < b) : (a ^ 2 + b)â»Â¹ * (a ^ 2 - b) < 1 :=
begin
  refine (inv_mul_lt_iff' _).mpr _,
  { nlinarith },
  linarith
end

lemma abs_sq_eq (a : â„) : (abs a) ^ 2 = a ^ 2 :=
begin
  by_cases h : 0 â‰¤ a,
  { simp [abs_of_nonneg h] },
  { simp [abs_of_neg (not_le.mp h)] }
end



end


namespace inner_product_space
/-! Another batch of lemmas for `analysis.normed_space.inner_product`, these ones specific to
projections onto singletons -/

variables {ğ•œ : Type*} [is_R_or_C ğ•œ]
variables {E : Type*} [inner_product_space ğ•œ E]


lemma projR_eq' {v : E} (hv : v â‰  0) (w : E) :
  ((@inner ğ•œ _ _ v w) / âˆ¥vâˆ¥ ^ 2) â€¢ v = orthogonal_projection (submodule.span ğ•œ {v}) w :=
begin
  apply eq_orthogonal_projection_of_mem_of_inner_eq_zero,
  { rw submodule.mem_span_singleton,
    use (@inner ğ•œ _ _ v w) / âˆ¥vâˆ¥ ^ 2 },
  intros x hx,
  rw submodule.mem_span_singleton at hx,
  obtain âŸ¨c, rflâŸ© := hx,
  have hv' : â†‘âˆ¥vâˆ¥ ^ 2 = @inner ğ•œ _ _ v v := by { norm_cast, simp [norm_sq_eq_inner] },
  have hv'' : @inner ğ•œ _ _ v v â‰  0 := hv âˆ˜ inner_self_eq_zero.mp,
  have h_div := div_mul_cancel _ hv'',
  simp [inner_sub_left, inner_smul_left, inner_smul_right, is_R_or_C.conj_div, conj_sym, hv'],
  right,
  rw h_div,
  simp [sub_self],
end

lemma projR_eq {v : E} (hv : âˆ¥vâˆ¥ = 1) (w : E) :
  (@inner ğ•œ _ _ v w) â€¢ v = orthogonal_projection (submodule.span ğ•œ {v}) w :=
begin
  have hv' : v â‰  0,
  { intros h,
    rw â† norm_eq_zero at h,
    rw hv at h,
    norm_num at h },
  convert projR_eq' hv' w,
  rw hv,
  simp
end

variables [complete_space E]

lemma sum_proj'' (v w : E) (hv : âˆ¥vâˆ¥ = 1) :
  (@inner ğ•œ _ _ v w) â€¢ v + (orthogonal_projection (submodule.span ğ•œ {v}).orthogonal w) = w :=
by simp [projR_eq hv, sum_proj']



lemma pyth_proj_sq' {v : E} (hv : âˆ¥vâˆ¥ = 1) (w : E) :
  âˆ¥wâˆ¥ ^2 = (is_R_or_C.abs (@inner ğ•œ _ _ v w)) ^ 2
    + âˆ¥orthogonal_projection_compl (submodule.span ğ•œ {v}) wâˆ¥ ^ 2 :=
begin
  rw â† is_R_or_C.norm_eq_abs,
  convert pyth_proj_sq w using 2,
  have := congr_arg norm (projR_eq hv w),
  rw norm_smul at this,
  rw hv at this,
  simp at this,
  rw this,
  refl
end


end inner_product_space


variables {E : Type*} [inner_product_space â„ E] [complete_space E]
variables (v : E)

open inner_product_space submodule

abbreviation orthog : submodule â„ E := (span â„ {v}).orthogonal

lemma prod_zero_left {w : E} (hw : w âˆˆ orthog v) : âŸªw, vâŸ«_â„ = 0 :=
inner_left_of_mem_orthogonal (mem_span_singleton_self v) hw

lemma prod_zero_right {w : E} (hw : w âˆˆ orthog v) : âŸªv, wâŸ«_â„ = 0 :=
inner_right_of_mem_orthogonal (mem_span_singleton_self v) hw

abbreviation projR : E â†’L[â„] â„ := inner_left v

abbreviation proj' : E â†’L[â„] (orthog v) :=
orthogonal_projection_compl (span â„ {v})

lemma pyth_proj_sq'' {v : E} (hv : âˆ¥vâˆ¥ = 1) (w : E) :
  âˆ¥wâˆ¥ ^2 = (inner v w) ^ 2 + âˆ¥orthogonal_projection_compl (submodule.span â„ {v}) wâˆ¥ ^ 2 :=
begin
  convert pyth_proj_sq' hv w using 2,
  simp [abs_sq_eq]
end

lemma sphere_inter_hyperplane {v : E} (hv : âˆ¥vâˆ¥ = 1) {x : sphere (0:E) 1} (hx : projR v x = 1) :
  x = âŸ¨v, by simp [hv]âŸ© :=
begin
  have hx' : âˆ¥â†‘xâˆ¥ = 1 := inner_product_space.mem_sphere_zero.mp x.2,
  ext,
  simpa [â† inner_eq_norm_mul_iff_of_mem_sphere hv hx'] using hx
end

lemma sphere_inter_hyperplane'' {v : E} (hv : âˆ¥vâˆ¥ = 1) {x : sphere (0:E) 1} (hx : â†‘x â‰  v) :
  projR v x < 1 :=
begin
  refine (inner_lt_one_iff_of_mem_sphere hv _).mpr hx,
  simpa [inner_product_space.mem_sphere_zero] using x.2
end


lemma sphere_inter_hyperplane'  {v : E} (hv : âˆ¥vâˆ¥ = 1) :
  ({âŸ¨v, by simp [hv]âŸ©}á¶œ : set (sphere (0:E) 1)) âŠ† coe â»Â¹' {w : E | projR v w â‰  1} :=
Î» w h, h âˆ˜ (sphere_inter_hyperplane hv)


/-- Stereographic projection, forward direction. This is a map from an inner product space `E` to
the orthogonal complement of an element `v` of `E`. It is smooth away from the affine hyperplane
through `v` parallel to the orthogonal complement.  It restricts on the sphere to the stereographic
projection. -/
def stereo_to_fun (x : E) : orthog v := (2 / (1 - projR v x)) â€¢ proj' v x

lemma stereo_to_fun_continuous_on : continuous_on (stereo_to_fun v) {x : E | projR v x â‰  1} :=
begin
  refine continuous_on.smul _ _,
  { refine continuous_const.continuous_on.div _ _,
    { exact continuous.continuous_on (continuous_const.sub (projR v).continuous) },
    intros x h h',
    apply h,
    linarith },
  { convert (proj' v).continuous.continuous_on }
end

def stereo_inv_fun_aux (w : E) : E := (âˆ¥wâˆ¥ ^ 2 + 4)â»Â¹ â€¢ ((4:â„) â€¢ w + (âˆ¥wâˆ¥ ^ 2 - 4) â€¢ v)

variables {v}

lemma stereo_inv_fun_aux_mem (hv : âˆ¥vâˆ¥ = 1) {w : E} (hw : w âˆˆ orthog v) :
  stereo_inv_fun_aux v w âˆˆ (sphere (0:E) 1) :=
begin
  have hâ‚ : 0 â‰¤ âˆ¥wâˆ¥ ^ 2 + 4 := by nlinarith,
  suffices : âˆ¥stereo_inv_fun_aux v wâˆ¥ = 1,
  { rwa inner_product_space.mem_sphere_zero },
  suffices : âˆ¥(4:â„) â€¢ w + (âˆ¥wâˆ¥ ^ 2 - 4) â€¢ vâˆ¥ = âˆ¥wâˆ¥ ^ 2 + 4,
  { rw stereo_inv_fun_aux,
    rw norm_smul,
    rw real.norm_eq_abs,
    rw abs_inv,
    rw this,
    have hâ‚‚ : âˆ¥wâˆ¥ ^ 2 + 4 â‰  0 := ne_of_gt (by nlinarith),
    rw abs_of_nonneg hâ‚,
    field_simp [hâ‚‚] },
  suffices : âˆ¥(4:â„) â€¢ w + (âˆ¥wâˆ¥ ^ 2 - 4) â€¢ vâˆ¥ ^ 2 = (âˆ¥wâˆ¥ ^ 2 + 4) ^ 2,
  { have hâ‚ƒ : 0 â‰¤ âˆ¥stereo_inv_fun_aux v wâˆ¥ := norm_nonneg _,
    simpa [hâ‚, hâ‚ƒ, -one_pow] using this },
  rw norm_add_pow_two_real,
  simp [norm_smul],
  rw inner_smul_left,
  rw inner_smul_right,
  rw prod_zero_left _ hw,
  simp,
  ring,
  rw real.norm_eq_abs,
  rw real.norm_eq_abs,
  rw abs_sq_eq,
  rw abs_sq_eq,
  rw hv,
  ring,
end

/-- Stereographic projection, reverse direction.  This is a map from the orthogonal complement of a
unit vector `v` in an inner product space `E` to the unit sphere in `E`. -/
def stereo_inv_fun (hv : âˆ¥vâˆ¥ = 1) (w : orthog v) : sphere (0:E) 1 :=
âŸ¨stereo_inv_fun_aux v (w:E), stereo_inv_fun_aux_mem hv w.2âŸ©

@[simp] lemma stereo_inv_fun_apply (hv : âˆ¥vâˆ¥ = 1) (w : orthog v) :
  (stereo_inv_fun hv w : E) = (âˆ¥wâˆ¥ ^ 2 + 4)â»Â¹ â€¢ ((4:â„) â€¢ w + (âˆ¥wâˆ¥ ^ 2 - 4) â€¢ v) :=
rfl

lemma mem_north_pole_compl (hv : âˆ¥vâˆ¥ = 1) (w : orthog v) :
  stereo_inv_fun hv w âˆˆ ({âŸ¨v, by simp [hv]âŸ©} : set (sphere (0:E) 1))á¶œ :=
begin
  suffices : (stereo_inv_fun hv w : E) â‰  v,
  { intros h,
    apply this,
    simp only [set.mem_singleton_iff] at h,
    rw h,
    refl },
  have hv' : âŸªv, vâŸ«_â„ = 1,
  { simp [hv, real_inner_self_eq_norm_square] },
  suffices : âŸªv, stereo_inv_fun hv wâŸ«_â„ < 1,
  { intros contra,
    rw contra at this,
    linarith },
  convert foo (âˆ¥(w : E)âˆ¥) (by norm_num : (0:â„) < 4),
  have hwv : âŸªv, â†‘wâŸ«_â„ = 0 := prod_zero_right v w.2,
  rw stereo_inv_fun_apply,
  simp [inner_add_right, inner_smul_right, hv', hwv],
  refl,
end

lemma continuous_stereo_inv_fun (hv : âˆ¥vâˆ¥ = 1) :
  continuous (stereo_inv_fun hv) :=
begin
  let c : sphere (0:E) 1 â†’ E := coe,
  suffices : continuous (c âˆ˜ (stereo_inv_fun hv)),
  { exact continuous_induced_rng this },
  have hâ‚€ : continuous (Î» w : E, âˆ¥wâˆ¥ ^ 2) := (continuous_pow 2).comp continuous_norm,
  have hâ‚ : continuous (Î» w : E, (âˆ¥wâˆ¥ ^ 2 + 4)â»Â¹),
  { refine (hâ‚€.add continuous_const).inv' _,
    intros w,
    refine ne_of_gt _,
    nlinarith },
  have hâ‚‚ : continuous (Î» w, (4:â„) â€¢ w + (âˆ¥wâˆ¥ ^ 2 - 4) â€¢ v),
  { refine (continuous_const.smul continuous_id).add _,
    refine (hâ‚€.sub continuous_const).smul continuous_const },
  convert (hâ‚.smul hâ‚‚).comp continuous_subtype_coe
end

lemma stereo_left_inv (hv : âˆ¥vâˆ¥ = 1) {x : sphere (0:E) 1} (hx : (x:E) â‰  v) :
  stereo_inv_fun hv (stereo_to_fun v x) = x :=
begin
  ext,
  simp only [stereo_to_fun, stereo_inv_fun_apply, norm_smul, smul_add, coe_smul, real.norm_two, normed_field.norm_div],
  set a := projR v x,
  set y := proj' v x,
  have split : (x : E) = y + a â€¢ v,
  { rw add_comm,
    exact (sum_proj'' v x hv).symm },
  have pyth : a ^ 2 + âˆ¥yâˆ¥ ^ 2 = 1,
  { convert (pyth_proj_sq'' hv x).symm using 2,
      have hx' : âˆ¥â†‘xâˆ¥ = 1 := inner_product_space.mem_sphere_zero.mp x.2,
    simp [hx'] },
  have ha : a < 1 := sphere_inter_hyperplane'' hv hx,
  have ha' : 1 - a â‰  0 := by linarith,
  have ha''' : âˆ¥1 - aâˆ¥ ^ 2 = (1 - a) ^ 2 := by rw [real.norm_eq_abs, abs_sq_eq],
  have h_denom : (2 / âˆ¥1 - aâˆ¥ * âˆ¥yâˆ¥) ^ 2 + 4 â‰  0,
  { refine ne_of_gt _,
    nlinarith },
  rw split,
  congr' 1,
  { rw â† mul_smul,
    rw â† mul_smul,
    convert one_smul â„ â†‘y,
    { field_simp [ha'],
      refine (div_eq_iff _).mpr _,
      { intros h,
        apply ha',
        refine (mul_eq_zero.mp h).elim id (Î» h', _),
        suffices : (1 - a) ^ 2 = 0,
        { exact pow_eq_zero this },
        have : 4 * âˆ¥yâˆ¥ ^ 2 + 4 * (1 - a) ^ 2 = 0 := by linarith,
        have : (1 - a) ^ 2 â‰¥ 0 := pow_two_nonneg (1 - a),
        linarith },
      nlinarith } },
  { rw â† mul_smul,
    congr' 1,
    rw â† div_eq_inv_mul,
    refine (div_eq_iff h_denom).mpr _,
    field_simp [ha''', ha'],
    nlinarith }
end

lemma inner_left_self {v : E} (hv : âˆ¥vâˆ¥ = 1) : inner_left v v = (1:â„) :=
by simp [real_inner_self_eq_norm_square, hv]

lemma inner_left_orthogonal (v : E) {w : E} (hw : w âˆˆ (submodule.span â„ ({v} : set E)).orthogonal) :
  @inner_left â„ E _ _ v w = (0:â„) :=
hw _ (submodule.mem_span_singleton_self v)

lemma proj_orthogonal_singleton (v : E) :
  orthogonal_projection (submodule.span â„ ({v} : set E)).orthogonal v = 0 :=
begin
  symmetry,
  ext,
  apply eq_orthogonal_projection_of_mem_of_inner_eq_zero',
  { simp },
  { simp [submodule.mem_span_singleton_self] },
end

lemma proj_orthogonal (v : E) {w : E} (hw : w âˆˆ (submodule.span â„ ({v} : set E)).orthogonal) :
  â†‘(orthogonal_projection (submodule.span â„ ({v} : set E)).orthogonal w) = w :=
orthogonal_projection_mem_subspace_eq_self hw

lemma stereo_right_inv (hv : âˆ¥vâˆ¥ = 1) (w : orthog v) :
  (stereo_to_fun v âˆ˜ coe) (stereo_inv_fun hv w) = w :=
begin
  have hâ‚ : proj' v v = 0 := proj_orthogonal_singleton v,
  have hâ‚‚ : proj' v w = w := by simpa using orthogonal_projection_mem_subspace_eq_self w.2,
  have hâ‚ƒ : inner_left v w = (0:â„) := inner_left_orthogonal v w.2,
  have hâ‚„ : inner_left v v = (1:â„) := inner_left_self hv,
  simp only [stereo_to_fun, stereo_inv_fun, stereo_inv_fun_aux, function.comp_app],
  simp only [hâ‚, hâ‚‚, hâ‚ƒ, hâ‚„, add_zero, continuous_linear_map.map_add, zero_add,
  subtype.coe_mk, mul_zero, smul_zero, continuous_linear_map.map_smul],
  rw â† mul_smul,
  rw â† mul_smul,
  convert one_smul â„ w,
  have h_denom : âˆ¥(w:E)âˆ¥ ^ 2 + 4 â‰  0 := by nlinarith,
  field_simp [h_denom],
  ring
end

/-- Stereographic projection from the unit sphere in `E`, centred at a unit vector `v` in `E`; this
is the version as a local homeomorphism. -/
def stereographic (hv : âˆ¥vâˆ¥ = 1) : local_homeomorph (sphere (0:E) 1) (orthog v) :=
{ to_fun := (stereo_to_fun v) âˆ˜ coe,
  inv_fun := stereo_inv_fun hv,
  source := {âŸ¨v, by simp [hv]âŸ©}á¶œ,
  target := set.univ,
  map_source' := by simp,
  map_target' := Î» w _, mem_north_pole_compl hv w,
  left_inv' := begin
    intros x hx,
    apply stereo_left_inv hv,
    intros hx',
    apply hx,
    simp [â† hx']
  end,
  right_inv' := Î» w _, stereo_right_inv hv w,
  open_source := is_open_compl_singleton,
  open_target := is_open_univ,
  continuous_to_fun := (stereo_to_fun_continuous_on v).comp continuous_subtype_coe.continuous_on
    (sphere_inter_hyperplane' hv),
  continuous_inv_fun := (continuous_stereo_inv_fun hv).continuous_on }


-- lemma submodule_eq_span_singletons [module ğ•œ E] (K : submodule ğ•œ E) :
--   K = â¨† (v : â†¥K), (submodule.span ğ•œ {â†‘v}) :=
-- begin
--   apply le_antisymm,
--   { rw le_supr_iff,
--     intros b h v hv,
--     exact h âŸ¨v, hvâŸ© (submodule.mem_span_singleton_self v) },
--   { apply @supr_le _ _ _ _ K,
--     rintros âŸ¨v, hvâŸ©,
--     exact (submodule.span_singleton_le_iff_mem v K).mpr hv }
-- end

-- lemma compl_eq_inter' (K : submodule ğ•œ E) : K.orthogonal = â¨… v : K, (submodule.span ğ•œ {(v:E)}).orthogonal :=
-- begin
--   conv_lhs { rw submodule_eq_span_singletons K },
--   rw â† submodule.infi_orthogonal (Î» v : K, submodule.span ğ•œ {(v:E)}),
-- end
